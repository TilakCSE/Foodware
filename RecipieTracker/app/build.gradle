plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

android {
    namespace = 'com.example.recipietracker'
    compileSdk = 34

    defaultConfig {
        applicationId = "com.example.recipietracker"
        minSdk = 24
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        def facebookAppId = localProperties.getProperty('FACEBOOK_APP_ID')
        resValue "string", "facebook_app_id", "\"${facebookAppId}\""
        resValue "string", "fb_login_protocol_scheme", "\"fb${facebookAppId}\""

        def facebookClientToken = localProperties.getProperty('FACEBOOK_CLIENT_TOKEN')
        resValue "string", "facebook_client_token", "\"${facebookClientToken}\""
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_9
        targetCompatibility = JavaVersion.VERSION_1_9
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.navigation:navigation-fragment:2.7.7'
    implementation 'androidx.navigation:navigation-ui:2.7.7'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:33.1.1')
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore'

    // Google Sign-In
    implementation 'com.google.android.gms:play-services-auth:21.2.0'

    // Facebook Sign-In
    implementation 'com.facebook.android:facebook-login:latest.release'

    // Glide for image loading
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

// NOTE: The key replacement script at the bottom does not need to be changed.